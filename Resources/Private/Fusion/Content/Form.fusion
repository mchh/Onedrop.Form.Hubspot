prototype(Onedrop.Form.Hubspot:FormRuntimeFactory) {
    @class = 'Onedrop\\Form\\Hubspot\\FusionObjects\\FormRuntimeFactory'

    noForm = Neos.Fusion:Tag {
        tagName = 'div'
        attributes {
            class = 'onedrophubspot_noform'
        }
        content = 'Please select a hubspot form in the element inspector'
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
    }

    formUnavailable = Neos.Fusion:Tag {
        tagName = 'div'
        attributes {
            class = 'onedrophubspot_noform'
        }
        content = 'The forms are temporarily not available'
        @process.contentElementWrapping = Neos.Neos:ContentElementWrapping
    }
}

prototype(Onedrop.Form.Hubspot:Content.Form) < prototype(Neos.Neos:ContentComponent) {
    formWrapperAttributes = Neos.Fusion:DataStructure {
        data-ajax = 'ajax-form'
        data-ajax-uri = Neos.Fusion:UriBuilder {
            package = 'Onedrop.Form.Hubspot'
            controller = 'AjaxForm'
            action = 'submit'
            format = 'html'
        }
        id = ${q(node).property('formIdentifier')}
    }

    content = Onedrop.Form.Hubspot:FormRuntimeFactory {
        identifier = ${q(node).property('formIdentifier')}
    }

    renderer = Neos.Fusion:Tag {
        tagName = 'div'

        attributes {
            data-ajax = ${props.formWrapperAttributes.data-ajax}
            data-ajax.@if.hasForm = ${_hasForm}
            data-ajax-uri = ${props.formWrapperAttributes.data-ajax-uri}
            data-ajax-uri.@if.hasForm = ${_hasForm}
            id = ${props.formWrapperAttributes.id}
            id.@if.hasNoForm = ${!_hasForm}
        }

        content = ${props.content}

        @context {
            _hasForm = ${String.indexOf(props.content, '<form') >= 0}
        }
    }

    @cache {
        mode = 'uncached'
        context {
            1 = 'node'
            2 = 'documentNode'
        }
    }

    @exceptionHandler = 'Onedrop\\Form\\Hubspot\\FusionObjects\\ExceptionHandler'
}

ajaxForm = Onedrop.Form.Hubspot:Content.Form
