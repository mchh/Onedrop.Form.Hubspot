prototype(Onedrop.Form.Hubspot:Component.Atom.DatePicker) < prototype(Onedrop.Form.Hubspot:Component.Atom.DefaultFormElement) {

    @styleguide {
        title = 'DatePicker'
        description = 'Input type date Form Element (input[type=date])'

        props {
            label = 'The label'
            id = 'the-id'
            class = 'form-control'
            errorClass = 'form-error'
            name = 'the-name'
            isRequired = false
        }

        propSets {
            'predefined value' {
                selectedValue = '2002-02-22'
            }
            'With two validation errors' {
                validationErrors {
                    0 = Neos.Fusion:DataStructure {
                        code = '1221560910'
                    }
                    1 = Neos.Fusion:DataStructure {
                        code = '1221560910'
                    }
                }
            }
        }
    }

    wrapperClass = 'date'

    renderer = afx`
        <input
            type="date"
            id={props.id}
            class={props.class}
            name={props.name}
            required={props.isRequired}
            value={Type.instance(props.value, '\DateTime') ? Date.format(props.value, 'Y-m-d') : props.value}
        />
    `
}
