prototype(Onedrop.Form.Hubspot:Component.Atom.SingleSelectRadiobuttons) < prototype(Onedrop.Form.Hubspot:Component.Atom.DefaultFormElement) {
    @styleguide {
        title = 'SingleSelectRadiobuttons'
        description = 'Single select radio buttons Form Element (input[type=radio])'

        props {
            label = 'The label'
            name = 'name'
            value = 'value2'
            isRequired = false
            properties {
                prependOptionLabel = '- select -'
                options = Neos.Fusion:DataStructure {
                    value1 = 'Label 01'
                    value2 = 'Label 02'
                    value3 = 'Label 03'
                }
            }
        }

        propSets {
            'With validation error' {
                validationErrors = Neos.Fusion:DataStructure {
                    0 = Neos.Fusion:DataStructure {
                        code = '1221560910'
                    }
                }
            }
            'With two validation errors' {
                validationErrors {
                    0 = Neos.Fusion:DataStructure {
                        code = '1221560910'
                    }
                    1 = Neos.Fusion:DataStructure {
                        code = '1221560910'
                    }
                }
            }
        }
    }

    wrapperClass = 'radio'

    renderer = afx`
        <ul>
            <Neos.Fusion:Loop items={props.properties.options} itemName="optionLabel" itemKey="optionValue" @children="itemRenderer">
                <li>
                    <label @if.hasLabel={optionLabel} for={"ident-" + optionValue}>
                        {optionLabel}
                    </label>
                    <input
                        type="radio"
                        id={"ident-" + optionValue}
                        name={props.name}
                        value={optionValue}
                        required={props.isRequired}
                        autocomplete={props.properties.autocomplete ? props.properties.autocomplete : false}
                        checked={Type.isArray(props.value) ? Array.indexOf(props.value, optionValue) > -1 : props.value == optionValue}
                    />
                </li>
            </Neos.Fusion:Loop>
        </ul>
    `
}
