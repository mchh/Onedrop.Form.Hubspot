prototype(Onedrop.Form.Hubspot:Component.Atom.DefaultFormElement) < prototype(Neos.Fusion:Component) {
    # API
    id = ''
    class = ''
    errorClass = ''
    name = ''
    label = ''
    value = null
    isRequired = false
    properties = Neos.Fusion:RawArray
    validationErrors = Neos.Fusion:RawArray
    wrapperClass = 'component'


    formRuntime = null
    containerElement = null
    # /API

    renderer.@process.defaultElementWrapping = afx`
        <Onedrop.Form.Hubspot:Component.Atom.ElementLabel id={props.id} label={props.label} isRequired={props.isRequired} />
        {value}
        <Onedrop.Form.Hubspot:Component.Atom.ElementValidationErrors validationErrors={props.validationErrors} />
    `

    renderer.@process.augment = Neos.Fusion:Augmenter {
        class = Neos.Fusion:Array {
            wrapperClass = ${props.wrapperClass}
            wrapperClass.@process.wrap = ${'a-form-' + value}

            required = ${this.wrapperClass + '--required'}
            required.@process.wrap = ${' ' + value}
            required.@if.isRequired = ${props.isRequired}

            error = ${this.wrapperClass + '--error'}
            error.@process.wrap = ${' ' + value}
            error.@if.isRequired = ${props.validationErrors}
        }
    }
}
